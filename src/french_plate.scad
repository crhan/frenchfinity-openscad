module french_plate_base () {
    cube([
        french_plate_width,
        french_plate_depth,
        french_plate_height
    ]);
}

module french_plate_with_tongue_and_groove () {
    y = (
        french_plate_height -
        frenchfinity_1_0_slot_distance_top - 
        frenchfinity_1_0_slot_total_height_calculated
    );

    module local_groove() {
        translate([0, 0, y])
            nut(french_plate_width, true);
    }

    module local_tongue() {
        translate([0, french_plate_depth, y])
            nut(french_plate_width, false);
    }

    union () {
        difference() {
            french_plate_base();
            local_groove();
        }
        local_tongue();
    }
}

module french_plate_with_tongue_and_groove_and_text () {
    labels = hintFileName([
        final_version_prefix_calculated,
        str("w", french_plate_width),
        str("h", french_plate_height),
        str("d", french_plate_depth)
    ]);

    difference() {
        french_plate_with_tongue_and_groove();
        // Back face, adaptively sized to stay below the cleat and inside the
        // part (the old fixed-size loop dropped lines on short plates).
        labelBlockVertical(
            labels, french_plate_width / 2, french_plate_depth,
            french_plate_width, 2, french_plate_height - 16
        );
    }
}

module feature_french_plate () {
    french_plate_with_tongue_and_groove_and_text();
}
